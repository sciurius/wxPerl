#############################################################################
## Name:        XS/Choicebook.xsp
## Purpose:     XS++ for Wx::Choicebook
## Author:      Mattia Barbon
## Modified by:
## Created:     30/07/2006
## RCS-ID:      $Id: Toolbook.xsp,v 1.1 2006/07/31 19:31:14 mbarbon Exp $
## Copyright:   (c) 2006 Mattia Barbon
## Licence:     This program is free software; you can redistribute it and/or
##              modify it under the same terms as Perl itself
#############################################################################

%module{Wx};

%typemap{wxToolbook*}{simple};

%{
#if WXPERL_W_VERSION_GE( 2, 7, 0 )

#include <wx/toolbar.h>
#include <wx/toolbook.h>
%}

%name{Wx::Toolbook} class wxToolbook
{
    %name{newDefault} wxToolbook()
        %code{% RETVAL = new wxToolbook();
                wxPli_create_evthandler( aTHX_ RETVAL, CLASS );
             %};
    %name{newFull} wxToolbook(wxWindow *parent,
                              wxWindowID id,
                              const wxPoint& pos = wxDefaultPosition,
                              const wxSize& size = wxDefaultSize,
                              long style = 0,
                              const wxString& name = wxEmptyString)
        %code{% RETVAL = new wxToolbook( parent, id, pos, size, style, name );
                wxPli_create_evthandler( aTHX_ RETVAL, CLASS );
             %};

    bool Create(wxWindow *parent,
                wxWindowID id,
                const wxPoint& pos = wxDefaultPosition,
                const wxSize& size = wxDefaultSize,
                long style = 0,
                const wxString& name = wxEmptyString);

    wxToolBarBase* GetToolBar();
    void Realize();
};

%{
void
new( ... )
  PPCODE:
    BEGIN_OVERLOAD()
        MATCH_VOIDM_REDISP( newDefault )
        MATCH_ANY_REDISP( newFull )
    END_OVERLOAD( "Wx::Toolbook::new" )
%}

%{
#endif
%}